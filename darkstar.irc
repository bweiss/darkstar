/*
 *      _               _          _
 *   __| |  __ _  _ __ | | __ ___ | |_   __ _  _ __
 *  / _` | / _` || '__|| |/ // __|| __| / _` || '__|
 * | (_| || (_| || |   |   < \__ \| |_ | (_| || |
 *  \__,_| \__,_||_|   |_|\_\|___/ \__| \__,_||_|
 *
 *  DarkStar - A modular script pack for the EPIC4 IRC client.
 *  Copyright (c) 2002 Brian Weiss <brian@epicsol.org>
 *
 *  Designed for use with EPIC4-1.1.7 and up.
 *  See the 'README' file for general information.
 *  See the 'COPYRIGHT' file for licensing information.
 */

/*
 * Warn user if client doesn't meet specifications
 */
if (!epic() || V < 20020819)
{
	xecho -b
	xecho -b WARNING!
	xecho -b
	xecho -b This script pack is for use with EPIC4, version 1.1.7 and up.
	xecho -b Your client, $J, does not meet this requirement.
	xecho -b Continue to use this script pack at your own risk.
	xecho -b
	xecho -b If you would like to obtain EPIC4, you can visit one of the
	xecho -b following sites.
	xecho -b
	xecho -b Official EPIC Homepage:     http://www.epicsol.org/
	xecho -b Official EPIC FTP site:     ftp://ftp.epicsol.org/
	xecho -b
	xecho -b Current versions of both DarkStar and EPIC4 can also be found
	xecho -b at the following URLs.
	xecho -b
	xecho -b http://darkstar.epicsol.org/
	xecho -b http://www.got.net/~brian/epic4/
	xecho -b

	^local pause $'Press any key to continue or "q" to quit.'
	if (pause == [q]) {
		xecho -b Bye!
		xecho -b
		//quit
	}
}


/*
 * Make sure we're clean
 */
dump all
timer -del all
for array in ($getarrays()) {
	@ delarray($array)
}


/*
 * Important global variables
 */
@ DS.VERSION = [0.2.8]
@ DS.INTERNAL_VERSION = [20020930]

/*
 * This will be incremented every time a change takes place in
 * the core scripts (this file and any .dsc files in core/).
 */
@ DS.CORE_ID = 73


eval xecho -b Loading DarkStar $DS.VERSION


/*
 * Find out where everything is and store it in the DS structure.
 */
if (getenv(DARKSTAR_HOME)) {
	@ DS.HOME = getenv(DARKSTAR_HOME)
} else {
	/* Attempt to get the directory from $loadinfo() */
	@ DS.HOME = before(-1 / $word(1 $loadinfo()))
	if (fexist($DS.HOME/darkstar.irc) == -1) {
		@ DS.HOME = [~/darkstar]
	}
}

if (getenv(DARKSTAR_USERDIR)) {
	@ DS.USER_DIR = getenv(DARKSTAR_USERDIR)
} else {
	@ DS.USER_DIR = [~/.darkstar]
}

if (getenv(DARKSTAR_RC)) {
	@ DS.RC = getenv(DARKSTAR_RC)
} else {
	@ DS.RC = [~/.dsrc]
}

@ DS.HELP_DIR = [$DS.HOME/help]
@ DS.MODULE_DIR = [$DS.HOME/modules $DS.USER_DIR/modules]
@ DS.STATUS_DIR = [$DS.HOME/status $DS.USER_DIR/status]
@ DS.THEME_DIR = [$DS.HOME/themes $DS.USER_DIR/themes]
@ DS.SAVE_DIR = [$DS.USER_DIR/save]


/*
 * Check for a user directory and create it if necessary.
 */
if (fexist($DS.USER_DIR) == -1)
{
	xecho -b Creating user directory: $DS.USER_DIR
	@ mkdir($DS.USER_DIR)
	@ mkdir($DS.USER_DIR/modules)
	@ mkdir($DS.USER_DIR/status)
	@ mkdir($DS.USER_DIR/themes)
	@ mkdir($DS.USER_DIR/save)

	/*
	 * This could be a first time user, so we'll display
	 * some extra information for them.
	 */
	xecho -b
	xecho -b Welcome to the DarkStar/EPIC4 script pack!
	xecho -b Copyright (c) 2002 Brian Weiss <brian@epicsol.org>
	xecho -b See the 'COPYRIGHT' file for details.
	xecho -b http://darkstar.epicsol.org/
	xecho -b
	xecho -b If you have not looked at the README yet, you should do so now.
	xecho -b
	xecho -b You are about to be presented with a list of modules and asked to
	xecho -b choose which of them you would like to have loaded. Just pressing
	xecho -b enter will load all the modules on the default auto-load list.
	xecho -b
	xecho -b After the modules have loaded, it is probably a good idea to have
	xecho -b a look around in the help browser (/dhelp).
	xecho -b
	xecho -b That's it! I hope you enjoy these scripts, a lot of effort has
	xecho -b gone into them. Please feel free to contact me if you have any
	xecho -b questions or comments. This message will not be displayed again.
	xecho -b

	^local pause $'Press any key to continue.'
}


/* 
 * Important /SET's
 * If you want to change these, it should be done via .dsrc
 */
eval set CLIENT_INFORMATION DarkStar $DS.VERSION
set -CONTINUED_LINE
set CONTROL_C_COLOR ON
set DISPLAY_PC_CHARACTERS 4
set FLOATING_POINT_MATH ON
set HIGH_BIT_ESCAPE 0
set INDENT ON


/*
 * Enable the new math parser.
 */
xdebug +NEW_MATH


/*
 * Change window name and level for the first window, if necessary.
 */
if (winnam(1) == [ircII]) {
	^window ircII name - level none
}


/*
 * Load the core scripts.
 */
eval load $DS.HOME/core/functions.dsc
eval load $DS.HOME/core/commands.dsc
eval load $DS.HOME/core/config.dsc
eval load $DS.HOME/core/help.dsc
eval load $DS.HOME/core/save.dsc
eval load $DS.HOME/core/status.dsc

/* Default settings for the core scripts */
config.add -b AUTO_LOAD_DEPENDENCIES 1
config.add    AUTO_LOAD_MODULES away channel dcc keepnick misc names nickcomp relay tabkey window
config.add -b AUTO_LOAD_PROMPT 1
config.add -b AUTO_SAVE_ON_UNLOAD 0
config.add -b LOADMODULE_VERBOSE 0
config.add -b SAVE_VERBOSE 0
config.add    THEME epic4

format.add MODLIST_FOOTER -------------------------------------------------------
format.add MODLIST_FOOTER1 $G Available modules: $numitems(modules), Loaded modules: $numitems(loaded_modules)
format.add MODLIST_FOOTER2
format.add MODLIST_HEADER #   Module           Version    Size  Loaded Auto-Load
format.add MODLIST_HEADER1 -------------------------------------------------------
format.add MODLIST_HEADER2
format.add MODLIST_MODULE  $[3]1 $[16]2 $[7]3 $[-7]4    ${[$5] ? [\[*\]] : [\[\ \]]}     ${[$6] ? [\[*\]] : [\[ \]]}
format.add SET $G Current value of $1 is $2-
format.add SET_NOVALUE $G No value for $1 has been set

if (fexist($DS.SAVE_DIR/core.sav) != -1) {
	^load $DS.SAVE_DIR/core.sav
}

eval load $DS.HOME/core/themes.dsc
eval load $DS.HOME/core/loader.dsc

if (fexist($DS.RC) != -1) {
	^load $DS.RC
}


if (fexist($DS.HOME/darkstar.irc) == -1) {
	xecho -b WARNING! We were unable to detect our location.
	xecho -b You may need to set the DARKSTAR_HOME environment variable.
	xecho -b Please have a look at the README file for more information.
}


/* EOF */