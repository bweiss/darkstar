/*
 *      _               _          _
 *   __| |  __ _  _ __ | | __ ___ | |_   __ _  _ __
 *  / _` | / _` || '__|| |/ // __|| __| / _` || '__|
 * | (_| || (_| || |   |   < \__ \| |_ | (_| || |
 *  \__,_| \__,_||_|   |_|\_\|___/ \__| \__,_||_|
 *
 *  DarkStar - A modular script pack for the EPIC4 IRC client.
 *  Copyright (c) 2002, 2003 Brian Weiss <brian@epicsol.org>
 *
 *  Designed for use with EPIC4-1.1.7 and up.
 *  See the 'README' file for general information.
 *  See the 'COPYRIGHT' file for licensing information.
 */

if (!epic() || V < 20020819) {
	echo
	echo WARNING!
	echo
	echo This script pack is for use with EPIC4, version 1.1.7 and up.
	echo Your client, $J, does not meet this requirement.
	echo Continue to use this script pack at your own risk.
	echo
	echo If you would like to obtain EPIC4, you can visit one of the
	echo following sites.
	echo
	echo Official EPIC Homepage:     http://www.epicsol.org/
	echo Official EPIC FTP site:     ftp://ftp.epicsol.org/
	echo
	echo Current versions of DarkStar and EPIC4 can also be found
	echo at the following URLs.
	echo
	echo http://darkstar.epicsol.org/
	echo http://darkstar.nuthouse.org/
	echo http://www.got.net/~brian/epic4/
	echo
	^local pause $'Press q to quit, or any other key to continue.'
	if (pause == [q]) {
		echo Bye!
		echo
		//quit
	}
}

/*
 * Make sure we're clean
 */
dump all
timer -del all
for array in ($getarrays()) {
	@ delarray($array)
}

/*
 * Important global variables
 */
@ DS.VERSION = [0.2.9]
@ DS.INTERNAL_VERSION = [20030905]

/*
 * This will be incremented every time a change takes place in
 * the core scripts (this file and any *.dsc files in $DS.HOME/core/).
 */
@ DS.CORE_ID = 86

eval xecho -b Loading DarkStar $DS.VERSION

/*
 * Find out where everything is and store it in the DS structure.
 */
@ DS.HOME       = getenv(DARKSTAR_HOME) ? getenv(DARKSTAR_HOME) : before(-1 / $word(1 $loadinfo()))
@ DS.USER_DIR   = getenv(DARKSTAR_USERDIR) ? getenv(DARKSTAR_USERDIR) : [~/.darkstar]
@ DS.RC         = getenv(DARKSTAR_RC) ? getenv(DARKSTAR_RC) : [~/.dsrc]
@ DS.HELP_DIR   = [$DS.HOME/help]
@ DS.MODULE_DIR = [$DS.HOME/modules $DS.USER_DIR/modules]
@ DS.STATUS_DIR = [$DS.HOME/status $DS.USER_DIR/status]
@ DS.THEME_DIR  = [$DS.HOME/themes $DS.USER_DIR/themes]
@ DS.SAVE_DIR   = [$DS.USER_DIR/save]

/*
 * Check for a "user directory" and create it if necessary.
 */
if (fexist($DS.USER_DIR) == -1) {
	xecho -b Creating user directory: $DS.USER_DIR
	@ mkdir($DS.USER_DIR)
	@ mkdir($DS.USER_DIR/modules)
	@ mkdir($DS.USER_DIR/status)
	@ mkdir($DS.USER_DIR/themes)
	@ mkdir($DS.USER_DIR/save)

	/*
	 * This could be a first time user, so we'll display
	 * some extra information for them.
	 */
	echo
	echo Welcome to the DarkStar/EPIC4 script pack!
	echo Copyright (c) 2002 Brian Weiss <brian@epicsol.org>
	echo See the 'COPYRIGHT' file for details.
	echo Official Website: http://darkstar.epicsol.org/
	echo
	echo If you have not looked at the README yet, you should do so now.
	echo
	echo You are about to be presented with a list of modules and asked to
	echo choose which of them you would like to have loaded. Just pressing
	echo enter will load all the modules on the default auto-load list.
	echo
	echo After the modules have loaded, it is probably a good idea to have
	echo a look around in the help browser (/DHELP).
	echo
	echo That's it! I hope you enjoy these scripts, a lot of effort has
	echo gone into them. Please feel free to contact me if you have any
	echo questions or comments. This message will not be displayed again.
	echo
	^local pause $'Press any key to continue.'
}

/* 
 * Some important /SETs
 * If you want to change these, it should be done via .dsrc
 */
eval set CLIENT_INFORMATION DarkStar $DS.VERSION
set CONTROL_C_COLOR ON
set DISPLAY_PC_CHARACTERS 4
set FLOATING_POINT_MATH ON
set HIGH_BIT_ESCAPE 0

/*
 * Enable the new math parser.
 */
xdebug +NEW_MATH

/*
 * Load all of the core scripts
 */
for fname in (globals.dsc functions.dsc commands.dsc help.dsc save.dsc config.dsc) {
	^load $DS.HOME/core/$fname
}

/*
 * The rest of the core scripts reference config and format variables
 * at load time, so we need to load the user's saved settings first.
 */
if (fexist($DS.SAVE_DIR/core.sav) != -1) {
	^load $DS.SAVE_DIR/core.sav
}

for fname in (status.dsc themes.dsc loader.dsc) {
	^load $DS.HOME/core/$fname
}

if (fexist($DS.RC) != -1) {
	^load $DS.RC
}

/* One final sanity check. This should never happen. */
if (fexist($DS.HOME/darkstar.irc) == -1) {
	echo WARNING! We were unable to detect our location.
	echo You may need to set the DARKSTAR_HOME environment variable.
	echo Please have a look at the README file for more information.
}


/* EOF */