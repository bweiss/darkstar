#version 1.0
/*
 * NICKCOMP.DSM - Nick completion module for Darkstar/EPIC4
 * Author: shade <shade@chemlab.org> - 1999
 *         Brian Weiss <brian@epicsol.org> - 2001
 *
 * Last modified: 2/17/02 (bmw)
 *
 * This script uses serial number 0 for all /on hooks.
 *
 * Most of this was taken from the nick completion module that shade wrote
 * for my BitchX/EPIC4 script called playdough. He deserves most of the
 * credit. Please send bug reports to me, not shade.
 */

queue cleanup.nickcomp
{
	^on input -"%: *"
}

config.add -b NICK_COMPLETION 1
config.add    NICK_COMPLETION_CHAR :

format.add NICK_COMPLETION $1: $2-


alias nickcomp.getnick
{
	for patt in (%$0% ?$0? %$0 ?$0 $0% $0? $0)
	{
		@ :findnick = pattern($patt $onchannel($C))
		@ function_return = findnick ? word(0 $findnick) : function_return
	}
}

on ^input "%: *"
{
	if (CONFIG[NICK_COMPLETION])
	{
		@ :complete = nickcomp.getnick($before($CONFIG.NICK_COMPLETION_CHAR $*))
		sendline ${complete ? fparse2(NICK_COMPLETION $complete $1-) : [$*]}
	}
}


/* bmw '01 */