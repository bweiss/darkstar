/*     _             _        _
 *  __| | __ _  _ _ | |__ ___| |_  __ _  _ _
 * / _` |/ _` || '_|| / /(_-<|  _|/ _` || '_|
 * \__,_|\__,_||_|  |_\_\/__/ \__|\__,_||_|
 *
 * MISC.DSM - The 'miscellaneous stuff' module for Darkstar/EPIC4
 * Author: Brian Weiss <brian@epicsol.org> - 2001
 *
 * Last modified: 10/14/01 (bmw)
 *
 * This script uses serial number 424 for all ON hooks.
 */

queue cleanup.misc
{
	for alias in (chat colors dns disco m nslookup pingme q umode ver whois wi wii ww)
	{
		^alias -$alias
	}

	for hook in (301 $jot(310 314) $jot(317 319) ctcp ctcp_reply dcc_chat msg notice send_ctcp send_dcc_chat send_msg send_notice)
	{
		^on $hook -"*"
	}

	^on #connect 424 -"*"
	^on mode -'\$servernick\(\) \$servernick\(\) *' 
	^on #timer 424 -"*"
	^on who -"* % % % % *"

	@ delarray(away_users)
}

alias chat (nick, void)
{
        if (nick)
        {
                dcc chat $nick
        }{
                xecho -b Usage: /chat <nick>
                xecho -b   - Attempts a DCC chat with <nick>
        }
}

alias colors (void)
{
	xecho -b Ctrl-C color codes:
	echo 0\0 1\1 2\2 3\3 4\4 5\5 6\6 7\7 8\8 9\9 10\10 11\11 12\12 13\13 14\14 15\15 16\16
}

alias dns (nicks)
{
	if (nicks)
	{
		for nick in ($nicks)
		{
			^local address
			
			if (is_on($nick))
			{
				@ address = after(-1 @ $userhost($nick))
			}{
				xecho -b User $nick is not online
			}
		
			xecho -b $nick!$userhost($nick) is ${convert($address) ? convert($address) : [unknown]}
		}
	}{
		xecho -b Usage: /dns <nick> [nick2] [nick3] ...
	}
}

alias disco disconnect

alias m msg

alias nslookup (address, void)
{
	if (address)
	{
		xecho -b $address is ${convert($address) ? convert($address) : [unknown]}
	}{
		xecho -b Usage: /nslookup <address>
	}
}

alias pingme (void)
{
	//ping $servernick()
}

alias q query

alias umode (mode)
{
	//mode $servernick() $mode
}

alias ver (nick default "$servernick()", void)
{
	ctcp $nick version
}

alias whois (args)
{
	@ :item = matchitem(away_users $word(0 $args)*)
	if (item > -1)
	{
		@ delitem(away_users $item)
	}

	//whois $args
}

alias wi (nick default "$servernick()", void)
{
	whois $nick
}

alias wii (nick default "$servernick()", void)
{
	whois $nick $nick
}

alias ww whowas


/*
 * WHOIS/WHOWAS related hooks.
 */

on #-timer 424 "*"
{
	^local delete

	for cnt from 0 to ${numitems(away_users) - 1}
	{
		@ :ts = word(1 $getitem(away_users $cnt))
		if ((time() - ts) > CONFIG[SHOW_AWAY_ONCE_TIMEOUT])
		{
			@ push(delete $cnt)
		}
	}

	for item in ($reverse($delete))
	{
		@ delitem(away_users $item)
	}
}

on ^301 "*"
{
	if (CONFIG[SHOW_AWAY_ONCE])
	{
		if (matchitem(away_users $0*) == -1)
		{
			echo $fparse(WHOIS_AWAY $*)
			@ setitem(away_users $numitems(away_users) $0 $time())
		}
	}{
		echo $fparse(WHOIS_AWAY $*)
	}
}

on ^310 "*" #

on ^311 "*"
{
	if (FORMAT[WHOIS_HEADER])
	{
		echo $fparse(WHOIS_HEADER)
	}
	echo $fparse(WHOIS_NICK $1 $2 $3)
	echo $fparse(WHOIS_NAME $5-)
}

on ^312 "*" echo $fparse(WHOIS_SERVER $2-)

on ^313 "*" echo $fparse(WHOIS_OPER $1)

on ^314 "*"
{
	echo $fparse(WHOWAS_NICK $1 $2 $3)
	echo $fparse(WHOWAS_NAME $5-)
}

on ^317 "*"
{
	echo $fparse(WHOIS_IDLE $2)
	echo $fparse(WHOIS_SIGNON $strftime($3 %c))
}

on ^318 "*"
{
	if (FORMAT[WHOIS_FOOTER])
	{
		echo $fparse(WHOIS_FOOTER $1-)
	}
}

on ^319 "*" echo $fparse(WHOIS_CHANNELS $2-)

/*
 * Default user modes
 */
on #^connect 424 "*" //mode $servernick() $CONFIG.DEFAULT_UMODES

/*
 * Other /FSET related hooks
 */
on ^ctcp "*" xecho -b $fparse(CTCP $*)
on ^ctcp_reply "*" xecho -b $fparse(CTCP_REPLY $*)
on ^dcc_chat "*" echo $fparse(DCC_CHAT $*)
on ^mode '$servernick() $servernick() *' xecho -b $fparse(UMODE $*)
on ^msg "*" echo $fparse(MSG $0 $userhost() $1-)
on ^notice "*" echo $fparse(NOTICE $0 $userhost() $1-)
on ^send_ctcp "*" xecho -b $fparse(SEND_CTCP $*)
on ^send_dcc_chat "*" echo $fparse(SEND_DCC_CHAT $*)
on ^send_msg "*" echo $fparse(SEND_MSG $*)
on ^send_notice "*" echo $fparse(SEND_NOTICE $*)

/* /WHO written by Da5id (thanks) */
on ^who "* % % % % *"
{
	echo $cparse(%c$[10]0%n $[9]1 $[3]2 %W$6%n${([$5]=~[*$after(. $S)*]) ? [%R$[14]after(1 . $5)%n] : [%c$[14]after(1 . $5)%n]} $[-10]3%W@%n$4)
}


/* bmw '01 */