#version 0.1.1
/* $Id$ */
/*
 * misc.dsm - The 'miscellaneous stuff' module for Darkstar/EPIC4
 *
 * Written by Brian Weiss
 * Copyright (c) 2002 Brian Weiss
 * See the 'COPYRIGHT' file for more information.
 *
 * This script uses serial numbers 0 and 424 for /on hooks.
 */

queue cleanup.misc
{
	for alias in (chat colors dns disco m nslookup pingme q umode ver whois wi wii ww)
	{
		^alias -$alias
	}

	for hook in (301 $jot(310 314) $jot(317 320) ctcp ctcp_reply dcc_chat msg notice send_dcc_chat send_msg send_notice)
	{
		^on $hook -"*"
	}

	^on action -'% \$servernick() *'
	^on #connect 424 -"*"
	^on mode -'\$servernick() \$servernick() *' 
	^on send_ctcp -"PRIVMSG *"
	^on #timer 424 -"*"
	^on who -"* % % % % *"

	@ delarray(away_users)
}


config.add    DEFAULT_UMODES iw
config.add -b SHOW_AWAY_ONCE 1
config.add    SHOW_AWAY_ONCE_TIMEOUT 900

format.add ACTION_USER *> $1 $3-
format.add CTCP $G CTCP $3 from $1 to $2: $4-
format.add CTCP_REPLY $G CTCP $3 reply from $1: $4-
format.add DCC_CHAT [$Z] %W.%n$1%W[%cdcc%W]%n $2-
format.add MSG [$Z] %W.%n$1%W[%c$2%W]%n $3-
format.add NOTICE [$Z] -$1%c\(%n$2%c\)%n- $3-
format.add SEND_ACTION_USER * -> $1: $servernick() $2-
format.add SEND_CTCP $G Requesting a CTCP $3 from $2: $4-
format.add SEND_DCC_CHAT [$Z] %W.%ndcc%W[%n$1%W]%n $2-
format.add SEND_MSG [$Z] %W.%nmsg%W[%n$1%W]%n $2-
format.add SEND_NOTICE [$Z] %W.%nnotice%c[%n$1%c]%n $2-
format.add WHO %c$[10]1%n $[9]2 $[3]3 %W$7%n${([$6]=~[*$after(. $servername())*]) ? [%R$[14]after(1 . $6)%n] : [%c$[14]after(1 . $6)%n]} $[-10]4%W@%n$5
format.add WHOIS_AWAY %c[**]%n away       %c:%n $1 - $2-
format.add WHOIS_CHANNELS %c[**]%n channels   %c:%n $3-
format.add WHOIS_FOOTER
format.add WHOIS_HEADER
format.add WHOIS_IDENTIFIED %c[**]%n identified %c:%n $2-
format.add WHOIS_IDLE %c[**]%n idle       %c:%n $tdiff($3)
format.add WHOIS_NAME %c[**]%n ircname    %c:%n $6-
format.add WHOIS_NICK %c[**]%n $2 %c\(%n$3@$4%c\)%n
format.add WHOIS_OPER %c[**]%n operator   %c:%n $2 is an IRC Operator
format.add WHOIS_SERVER %c[**]%n server     %c:%n $3 \($4-\)
format.add WHOIS_SIGNON %c[**]%n signon     %c:%n $strftime($4 %%a %%b %%d %%T %%Z %%Y)
format.add WHOWAS_NAME %c[**]%n ircname    %c:%n $6-
format.add WHOWAS_NICK %c[**]%n $2 was %c\(%n$3@$4%c\)%n
format.add UMODE $G Mode change \"$3-\" for user $2 by $1


alias chat (nick, void)
{
	if (!nick)
	{
		xecho -b Usage: /chat <nick>
		return
	}

	//dcc chat $nick
}

alias colors (void)
{
	xecho -b Ctrl-C color codes:
	echo 0zero 1one 2two 3three 4four 5five 6six 7seven 8eight 9nine 10ten
      echo 11eleven 12twelve 13thirteen 14fourteen 15fifteen 16sixteen
}

alias disco disconnect

alias dns (nicks)
{
	if (!nicks)
	{
		xecho -b Usage: /dns <nick> ...
		return
	}

	for nick in ($nicks)
	{
		^local address

		if (is_on($nick))
		{
			@ address = after(-1 @ $userhost($nick))
		}{
			xecho -b User $nick is not online
		}
		
		xecho -b $nick!$userhost($nick) is ${convert($address) ? convert($address) : [unknown]}
	}
}

alias m msg

alias nslookup
{
	if (![$*])
	{
		xecho -b Usage: /nslookup <address> ...
		return
	}

	for addr in ($*)
	{
		@ :tmp = convert($addr)
		xecho -b $addr is ${tmp ? tmp : [unknown]}
	}
}

alias pingme (void)
{
	//ping $servernick()
}

alias q query

alias umode (mode)
{
	//mode $servernick() $mode
}

alias ver (nick default "$servernick()", void)
{
	ctcp $nick version
}

alias wi whois
alias whois (args)
{
	@ :item = matchitem(away_users $word(0 $args)*)
	if (item > -1)
	{
		@ delitem(away_users $item)
	}

	//whois $args
}

alias wii (nick default "$servernick()", void)
{
	whois $nick $nick
}

alias ww whowas


/*
 * WHOIS/WHOWAS related hooks.
 */
on #-timer 424 "*"
{
	^local delete

	for cnt from 0 to ${numitems(away_users) - 1}
	{
		@ :ts = word(1 $getitem(away_users $cnt))
		if ((time() - ts) > CONFIG[SHOW_AWAY_ONCE_TIMEOUT])
		{
			@ push(delete $cnt)
		}
	}

	for item in ($reverse($delete))
	{
		@ delitem(away_users $item)
	}
}

on ^301 "*"
{
	if (FORMAT[WHOIS_AWAY])
	{
		if (CONFIG[SHOW_AWAY_ONCE])
		{
			if (matchitem(away_users $0*) == -1)
			{
				echo $fparse(WHOIS_AWAY $*)
				@ setitem(away_users $numitems(away_users) $0 $time())
			}
		}{
			echo $fparse(WHOIS_AWAY $*)
		}
	}
}

on ^310 "*" #

on ^311 "*"
{
	if (FORMAT[WHOIS_HEADER])
	{
		echo $fparse(WHOIS_HEADER)
	}
	if (FORMAT[WHOIS_NICK])
	{
		echo $fparse(WHOIS_NICK $*)
	}
	if (FORMAT[WHOIS_NAME])
	{
		echo $fparse(WHOIS_NAME $*)
	}
}

on ^312 "*"
{
	if (FORMAT[WHOIS_SERVER])
	{
		echo $fparse(WHOIS_SERVER $*)
	}
}

on ^313 "*"
{
	if (FORMAT[WHOIS_OPER])
	{
		echo $fparse(WHOIS_OPER $*)
	}
}

on ^314 "*"
{
      if (FORMAT[WHOIS_HEADER])
      {
            echo $fparse(WHOIS_HEADER)
      }
	if (FORMAT[WHOWAS_NICK])
	{
		echo $fparse(WHOWAS_NICK $*)
	}
	if (FORMAT[WHOWAS_NAME])
	{
		echo $fparse(WHOWAS_NAME $*)
	}
}

on ^317 "*"
{
	if (FORMAT[WHOIS_IDLE])
	{
		echo $fparse(WHOIS_IDLE $*)
	}
	if (FORMAT[WHOIS_SIGNON])
	{
		echo $fparse(WHOIS_SIGNON $*)
	}
}

on ^318 "*"
{
	if (FORMAT[WHOIS_FOOTER])
	{
		echo $fparse(WHOIS_FOOTER $*)
	}
}

on ^319 "*"
{
	if (FORMAT[WHOIS_CHANNELS])
	{
		echo $fparse(WHOIS_CHANNELS $*)
	}
}

on ^320 "*"
{
	if (FORMAT[WHOIS_IDENTIFIED])
	{
		echo $fparse(WHOIS_IDENTIFIED $*)
	}
}

on ^369 "*"
{
	if (FORMAT[WHOIS_FOOTER])
	{
		echo $fparse(WHOIS_FOOTER $*)
	}
}

/*
 * Default user modes
 */
on #^connect 424 "*" //mode $servernick() $CONFIG.DEFAULT_UMODES

/*
 * Other /FSET related hooks
 */
on ^action '% $servernick() *'
{
	if (FORMAT[ACTION_USER])
	{
		echo $fparse(ACTION_USER $*)
	}
}

on ^ctcp "*"
{
	if (FORMAT[CTCP])
	{
		echo $fparse(CTCP $*)
	}
}

on ^ctcp_reply "*"
{
	if (FORMAT[CTCP_REPLY])
	{
		echo $fparse(CTCP_REPLY $*)
	}
}

on ^dcc_chat "*"
{
	if (FORMAT[DCC_CHAT])
	{
		echo $fparse(DCC_CHAT $*)
	}
}

on ^mode '$servernick() $servernick() *'
{
	if (FORMAT[UMODE])
	{
		echo $fparse(UMODE $*)
	}
}

on ^msg "*"
{
	if (FORMAT[MSG])
	{
		echo $fparse(MSG $0 $userhost() $1-)
	}
}

on ^notice "*"
{
	if (FORMAT[NOTICE])
	{
		echo $fparse(NOTICE $0 $userhost() $1-)
	}
}

on ^send_action "*"
{
	if (FORMAT[SEND_ACTION_USER])
	{
		echo $fparse(SEND_ACTION_USER $*)
	}
}

on ^send_ctcp "PRIVMSG *"
{
	if (FORMAT[SEND_CTCP] && [$2] != [ACTION])
	{
		echo $fparse(SEND_CTCP $*)
	}
}

on ^send_dcc_chat "*"
{
	if (FORMAT[SEND_DCC_CHAT])
	{
		echo $fparse(SEND_DCC_CHAT $*)
	}
}

on ^send_msg "*"
{
	if (FORMAT[SEND_MSG])
	{
		echo $fparse(SEND_MSG $*)
	}
}

on ^send_notice "*"
{
	if (FORMAT[SEND_NOTICE])
	{
		echo $fparse(SEND_NOTICE $*)
	}
}

on ^who "* % % % % *"
{
	echo $fparse(WHO $*)
}


/* EOF */