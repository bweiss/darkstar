#version 0.2
/* $Id$ */
/*
 * dcc.dsm - DCC related module for DarkStar/EPIC4
 * Copyright (c) 2002 Brian Weiss
 * See the 'COPYRIGHT' file for more information.
 *
 * This script uses serial numbers 0 and 450 for /on hooks.
 */

/****** CLEANUP QUEUE ******/

queue cleanup.dcc
{
	^alias -chat
	^on dcc_chat -"*"
	^on send_dcc_chat -"*"
	^on #dcc_request 450 -"% SEND *"
}


/****** CONFIG/FORMAT VARIABLES ******/

config.add -b DCC_AUTOGET 0

format.add DCC_CHAT =$1= $2-
format.add SEND_DCC_CHAT => =$1= $2-


/****** USER ALIASES ******/

alias chat (nick, void)
{
	if (!nick) {
		xecho -b Usage: /chat <nick>
		return
	}

	dcc chat $nick
}


/****** ON HOOKS ******/

/*
 * DCC auto-get.
 */
on #-dcc_request 450 "% SEND *"
{
	if (CONFIG.DCC_AUTOGET) {
		xecho -b DCC AutoGet: Accepting SEND of $2 from $0
		dcc get $0 $2
	}
}

on ^dcc_chat "*"
{
	if (FORMAT.DCC_CHAT) {
		echo $fparse(DCC_CHAT $*)
	}
}

on ^send_dcc_chat "*"
{
	if (FORMAT.SEND_DCC_CHAT) {
		echo $fparse(SEND_DCC_CHAT $*)
	}
}


/* EOF */