#version 0.5
/* $Id$ */
/*
 * window.dsm - Window module for DarkStar/EPIC4
 *
 * Written by Brian Weiss
 * Copyright (c) 2002 Brian Weiss
 * See the 'COPYRIGHT' file for more information.
 *
 * This script uses serial number 426 for all /on hooks.
 */

/****** CLEANUP QUEUE ******/

queue cleanup.window
{
	for alias in (swap wb wc wj wk wl wn wp wub) {
		^alias -$alias
	}
	for wref in ($winrefs()) {
		^alias -$wref
	}

	^on #hook 426 -"CONFIG MSG_WINDOW *"
	^on #hook 426 -"CONFIG MSG_WINDOW_LEVEL *"
	^on #hook 426 -"CONFIG MSG_WINDOW_SIZE *"
	^on #join 426 -'\$servernick() *'
	^on #leave 426 -'\$servernick() *'
	^on #window_create 426 -"*"
	^on #window_kill 426 -"msgs"
	^on #window_kill 426 -"*"

	for char in ($jot(0 9)) {
		^bind META1-$char NOTHING
	}
	for char in (n p h c k $jot(0 9)) {
		^bind META8-$char NOTHING
	}
	for keyb in (META1-^I META5-~ META6-~ META10-~ META11-~ META12-~ META13-~) {
		^bind $keyb NOTHING
	}
	^bind ^W NEXT_WINDOW
	^bind ^X META2_CHARACTER
	^bind -symbolic key_f11 NOTHING
	^bind -symbolic key_f12 NOTHING

	for item in ($getrmatches(single_status msgs)) {
		@ delitem(single_status $item)
	}
	if (winnum(msgs) != -1) {
		^window msgs kill
	}
}


/****** CONFIG/FORMAT VARIABLES ******/

config.add    AUTO_BIND_CHANNELS
config.add    AUTO_CREATE_WINDOWS 2
config.add -b BIND_ON_JOIN 0
config.add -b MSG_WINDOW 0
config.add    MSG_WINDOW_LEVEL msgs,opnotes,wallops
config.add    MSG_WINDOW_SIZE 5
config.add -b UNBIND_ON_PART 0


/****** INIT ******/

/*
 * Add "msgs" to the single_status array to prevent the msg window
 * from having double turned on.
 */
@ setitem(single_status $numitems(single_status) msgs)

/*
 * Key bindings.
 */
^bind ^W META8_CHARACTER
^bind ^X SWITCH_CHANNELS
^bind META1-^I parse_command ^window next
^bind META1-1 PARSE_COMMAND ^window swap 1
^bind META1-2 PARSE_COMMAND ^window swap 2
^bind META1-3 PARSE_COMMAND ^window swap 3
^bind META1-4 PARSE_COMMAND ^window swap 4
^bind META1-5 PARSE_COMMAND ^window swap 5
^bind META1-6 PARSE_COMMAND ^window swap 6
^bind META1-7 PARSE_COMMAND ^window swap 7
^bind META1-8 PARSE_COMMAND ^window swap 8
^bind META1-9 PARSE_COMMAND ^window swap 9
^bind META1-0 PARSE_COMMAND ^window swap 10
^bind META2-1 META10_CHARACTER
^bind META2-2 META12_CHARACTER
^bind META2-3 META13_CHARACTER
^bind META2-4 META11_CHARACTER
^bind META2-5 META5_CHARACTER
^bind META2-6 META6_CHARACTER
^bind META5-~ SCROLL_BACKWARD
^bind META6-~ SCROLL_FORWARD
^bind META8-n PARSE_COMMAND ^window next
^bind META8-p PARSE_COMMAND ^window previous
^bind META8-h PARSE_COMMAND ^window hide
^bind META8-c PARSE_COMMAND ^window new double on
^bind META8-k PARSE_COMMAND ^window kill
^bind META8-1 PARSE_COMMAND ^window swap 1
^bind META8-2 PARSE_COMMAND ^window swap 2
^bind META8-3 PARSE_COMMAND ^window swap 3
^bind META8-4 PARSE_COMMAND ^window swap 4
^bind META8-5 PARSE_COMMAND ^window swap 5
^bind META8-6 PARSE_COMMAND ^window swap 6
^bind META8-7 PARSE_COMMAND ^window swap 7
^bind META8-8 PARSE_COMMAND ^window swap 8
^bind META8-9 PARSE_COMMAND ^window swap 9
^bind META8-0 PARSE_COMMAND ^window swap 10
^bind META10-~ SCROLL_START
^bind META11-~ SCROLL_END
^bind META12-~ PARSE_COMMAND window.msgwin_up
^bind META13-~ PARSE_COMMAND window.msgwin_down
^bind -symbolic key_f11 TOGGLE_INSERT_MODE
^bind -symbolic key_f12 PARSE_COMMAND if (winnum(msgs) != -1) {window msgs clear}

/*
 * Force the loading of saved settings early
 * so the following /if commands can use them.
 */
module.load_saved_settings

/*
 * Add shortcut aliases for all existing windows.
 */
for wref in ($winrefs()) {
	^alias $wref window swap $wref
}


/****** USER ALIASES ******/

alias swap (winnum, void)
{
	if (winnum) {
		window swap $winnum
	} else {
		input "What window should we swap to? " {
			window swap $0
		}
	}
}

alias wb  (chan default "$C", void) {window bind $chan}
alias wc  (void)                    {window new_hide}
alias wj  (...)                     {window new channel "$*" hide}
alias wk  (...)                     {window kill}
alias wl  (...)                     {window last}
alias wn  (...)                     {window next}
alias wp  (...)                     {window previous}
alias wub (...)                     {window unbind}


/****** INTERNAL ALIASES ******/

alias window.msgwin_up (void)
{
	@:cwin = winnum()
	^window ref msgs
	parsekey SCROLL_BACKWARD
	^window ref $cwin
}

alias window.msgwin_down (void)
{
	@:cwin = winnum()
	^window ref msgs
	parsekey SCROLL_FORWARD
	^window ref $cwin
}


/****** ON HOOKS ******/

/*
 * Kill or create msg window on /dset MSG_WINDOW
 */
on #-hook 426 "CONFIG MSG_WINDOW *"
{
	if (CONFIG.MSG_WINDOW) {
		if (winnum(msgs) == -1) {
			window new name msgs double off
			window level $CONFIG.MSG_WINDOW_LEVEL
			window size $CONFIG.MSG_WINDOW_SIZE
			window last
		}
	} else {
		if (winnum(msgs) != -1) {
			window msgs kill
		}
	}
}

on #-hook 426 "CONFIG MSG_WINDOW_LEVEL *"
{
	if (CONFIG.MSG_WINDOW) {
		window msgs level $CONFIG.MSG_WINDOW_LEVEL
	}
}

on #-hook 426 "CONFIG MSG_WINDOW_SIZE *"
{
	if (CONFIG.MSG_WINDOW) {
		window msgs size $CONFIG.MSG_WINDOW_SIZE
	}
}

on #-join 426 '$servernick() *'
{
	if (CONFIG.BIND_ON_JOIN) {
		window bind $1
	}
}

on #-leave 426 '$servernick() *'
{
	if (CONFIG.UNBIND_ON_PART) {
		window unbind $1
	}
}

on #-window_create 426 "*"
{
	/* Add a shortcut alias for this window. */
	^alias $0 window swap $0

	if (STATUS.DOUBLE) {
		^window double on
	}
}

/*
 * Turn off MSG_WINDOW if someone manually kills the msgs window.
 */
on #-window_kill 426 "msgs"
{
	if (CONFIG.MSG_WINDOW) {
		dset MSG_WINDOW OFF
	}
}

on #-window_kill 426 "*"
{
	/* Remove shortcut alias for this window. */
	^alias -$0

	/* Reset the msgs window. */
	if (CONFIG.MSG_WINDOW) {
		window previous
		window show msgs
		window size $CONFIG.MSG_WINDOW_SIZE
		window last
	}
}


/****** STARTUP ******/

/*
 * Create some windows when the module loads.
 */
if (CONFIG.AUTO_CREATE_WINDOWS)
{
	for cnt from 1 to $CONFIG.AUTO_CREATE_WINDOWS {
		@:autobind_chan = word($cnt $CONFIG.AUTO_BIND_CHANNELS)
		if (winnum($cnt) == -1) {
			window new
		}
		if (autobind_chan) {
			window bind $autobind_chan
		}
		window hide
	}
}

if (CONFIG.MSG_WINDOW)
{
	if (winnum(msgs) == -1) {
		window new name msgs double off
		window size $CONFIG.MSG_WINDOW_SIZE
		window level $CONFIG.MSG_WINDOW_LEVEL
		window last
	}
}


/* EOF */